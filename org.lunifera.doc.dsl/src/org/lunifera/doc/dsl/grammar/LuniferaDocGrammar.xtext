/*******************************************************************************
 * Copyright (c) 2013 Loetz KG (Heidelberg), Petra Bierleutgeb and others.
 *  All rights reserved. This program and the accompanying materials
 *  are made available under the terms of the Eclipse Public License v1.0
 *  which accompanies this distribution, and is available at
 *  http://www.eclipse.org/legal/epl-v10.html
 ******************************************************************************/

grammar org.lunifera.doc.dsl.LuniferaDocGrammar with org.eclipse.xtext.xbase.Xbase

import "http://www.lunifera.org/luniferadoc" as luniferadoc
import "http://www.lunifera.org/luniferadoc/layout" as layout
import "http://www.lunifera.org/luniferadoc/document" as document
import "http://www.lunifera.org/luniferadoc/richstring" as richstring
import "http://www.lunifera.org/luniferadoc/markup" as markup
import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase

NamedDocument returns luniferadoc::NamedDocument:
	LuniferaDocLayout | LuniferaDocDocument;

LuniferaDocDocument returns document::LuniferaDocDocument:
	EntityDocument | DTODocument | BPMProcessDocument | BPMTaskDocument | VaaclipseViewDocument | UIDocument;
	
LuniferaDocLayout returns layout::LuniferaDocLayout:
	GeneralDocument | EntityLayout | DTOLayout | BPMProcessLayout | BPMTaskLayout | VaaclipseViewLayout | UILayout;

DocumentInclude returns luniferadoc::DocumentInclude:
	'include' include=[document::LuniferaDocDocument|QualifiedName] 'as' varName=ID;

enum IncludeType returns luniferadoc::DocType:
	ENTITY = 'ENTITY' | DTO = 'DTO' | BPM_PROCESS = 'BPM_PROCESS' | BPM_TASK = 'BPM_TASK' | 
	VAACLIPSE_VIEW = 'VAACLIPSE_VIEW' | UI = 'UI';

// LAYOUTS

EntityLayout returns layout::EntityLayout:
	'EntityLayout' name=ID '{'
	imports+=XImportDeclaration*
	content=RichString
	'}';
	
DTOLayout returns layout::DTOLayout:
	'DTOLayout' name=ID '{'
	imports+=XImportDeclaration*
	content=RichString
	'}';
	
BPMProcessLayout returns layout::BPMProcessLayout:
	'BPMProcessLayout' name=ID '{'
	imports+=XImportDeclaration*
	content=RichString
	'}';
	
BPMTaskLayout returns layout::BPMTaskLayout:
	'BPMTaskLayout' name=ID '{'
	imports+=XImportDeclaration*
	content=RichString
	'}';
	
VaaclipseViewLayout returns layout::VaaclipseViewLayout:
	'VaaclipseViewLayout' name=ID '{'
	imports+=XImportDeclaration*
	content=RichString
	'}';
	
UILayout returns layout::UILayout:
	'UILayout' name=ID '{'
	imports+=XImportDeclaration*
	content=RichString
	'}';
	
// DOCUMENTS
	
GeneralDocument returns document::GeneralDocument:
	'GeneralDocument' name=ID '{'
	imports+=XImportDeclaration*
	includes+=DocumentInclude*
	content=RichString
	'}';
	
EntityDocument returns document::EntityDocument:
	'EntityDocument' name=QualifiedName 'for' entityClass=QualifiedName '{'
	'description' '{' description=EntityDescription	'}'
	'fields' '{' fields=EntityFields '}'
	'}';
	
EntityDescription returns document::EntityDescription:
	{document::EntityDescription} content=RichString;
	
EntityFields returns document::EntityFields:
	{document::EntityFields} fields+=EntityField*;
	
EntityField returns document::EntityField:
	{document::EntityField} 'name' name=ID
	('type' type=ID)?
	('length' length=INT)?
	('pk' pk?="true")?
	('nullable' nullable?="true")?
	'description' '{' description=RichString '}';
	
DTODocument returns document::DTODocument:
	'DTODocument' name=QualifiedName 'for' dtoClass=QualifiedName '{'
	'description' '{' description=DTODescription '}'
	'details' '{' details=DTODetails '}'
	'}';

DTODescription returns document::DTODescription:
	{document::DTODescription} content=RichString;
	
DTODetails returns document::DTODetails:
	{document::DTODetails} properties+=DTOProperty*;
	
DTOProperty returns document::DTOProperty:
	{document::DTOProperty} 'name' name=ID
	'description' '{' description=RichString '}';

BPMProcessDocument returns document::BPMProcessDocument:
	'BPMProcessDocument';
	
BPMTaskDocument returns document::BPMTaskDocument:
	'BPMTaskDocument';

VaaclipseViewDocument returns document::VaaclipseViewDocument:
	'VaaclipseViewDocument';
	
UIDocument returns document::UIDocument:
	'UIDocument';

// Non-RichString Tags

//TagURL returns markup::TagURL:
//	'**'
//		'url' url=ID
//		'text' text=RichString
//	'**'
//;

// RichString

RichString returns richstring::RichString:
	{richstring::RichString} (expressions+=RichStringLiteral |
	expressions+=RichStringLiteralStart expressions+=RichStringPart?
	(expressions+=RichStringLiteralInbetween expressions+=RichStringPart?)*
	expressions+=RichStringLiteralEnd);

RichStringLiteral returns xbase::XExpression:
	{richstring::RichStringLiteral} value=RICH_TEXT;

RichStringLiteralStart returns xbase::XExpression:
	{richstring::RichStringLiteral} value=RICH_TEXT_START;

RichStringLiteralInbetween returns xbase::XExpression:
	{richstring::RichStringLiteral}
	(value=RICH_TEXT_INBETWEEN | value=COMMENT_RICH_TEXT_INBETWEEN);

RichStringLiteralEnd returns xbase::XExpression:
	{richstring::RichStringLiteral}
	(value=RICH_TEXT_END | value=COMMENT_RICH_TEXT_END);

InternalRichString returns xbase::XExpression:
	{richstring::RichString} (expressions+=RichStringLiteralInbetween (expressions+=RichStringPart?
	expressions+=RichStringLiteralInbetween)*);

RichStringPart returns xbase::XExpression:
	XExpressionInsideBlock |
	RichStringForLoop |
	RichStringIf |
	RichStringH1 | RichStringH2 | RichStringExample | RichStringURL;

RichStringExample returns xbase::XExpression:
	{richstring::RichStringExample}
	"example" expression=InternalRichString	"/example"
;

RichStringH1 returns xbase::XExpression:
	{richstring::RichStringH1}
	"h1" expression=InternalRichString "/h1"
;

RichStringH2 returns xbase::XExpression:
	{richstring::RichStringH2}
	"h2" expression=InternalRichString "/h2"
;

RichStringURL returns xbase::XExpression:
	{richstring::RichStringURL}
	"url" location=ID 
	text=InternalRichString
	"/url";

RichStringForLoop returns xbase::XExpression:
	{richstring::RichStringForLoop}
	"FOR" declaredParam=JvmFormalParameter ':' forExpression=XExpression
	("BEFORE" before=XExpression)?
	("SEPARATOR" separator=XExpression)?
	("AFTER" after=XExpression)?
	eachExpression=InternalRichString
	"ENDFOR";

RichStringIf returns xbase::XExpression:
	{richstring::RichStringIf}
	"IF" if=XExpression
	then=InternalRichString
	elseIfs+=RichStringElseIf*
	("ELSE"
	else=InternalRichString)?
	"ENDIF";

RichStringElseIf returns richstring::RichStringElseIf:
	"ELSEIF" if=XExpression then=InternalRichString;

terminal RICH_TEXT:
	"'''" IN_RICH_STRING* ("'''" | ("'" "'"?)? EOF);

terminal RICH_TEXT_START:
	"'''" IN_RICH_STRING* ("'" "'"?)? '«';

terminal RICH_TEXT_END:
	'»' IN_RICH_STRING* ("'''" | ("'" "'"?)? EOF);

terminal RICH_TEXT_INBETWEEN:
	'»' IN_RICH_STRING* ("'" "'"?)? '«';

terminal COMMENT_RICH_TEXT_INBETWEEN:
	"««" !('\n' | '\r')* ('\r'? '\n' IN_RICH_STRING* ("'" "'"?)? '«')?;

terminal COMMENT_RICH_TEXT_END:
	"««" !('\n' | '\r')* (('\r'? '\n' IN_RICH_STRING* ("'''" | ("'" "'"?)? EOF)) | EOF);

terminal fragment IN_RICH_STRING:
	"''" !('«' | "'")
	| "'" !('«' | "'")
	| !('«' | "'");